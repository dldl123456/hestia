buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "net.onedaybeard.artemis:artemis-odb-gradle-plugin:2.1.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
}
apply plugin: 'org.jetbrains.dokka'
apply from: 'dependencies.gradle'

dependencies {
    //Plugins compiles :server so root depends on both
    compile project(":plugins")
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: "artemis"

    group 'worlds.gregs.hestia'
    version '0.1.2'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile fileTree(dir: '../libs', includes: ['*.jar'])
        compile systemDependencies.values()
        compile utilityDependencies.values()
        compile mainDependencies.values()
        compile logDependencies.values()
        testCompile testDependencies.values()
    }

    weave {
        enableArtemisPlugin = true
        enablePooledWeaving = true
        optimizeEntitySystems = true
        classesDir = sourceSets.main.output.classesDirs.first()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

dokka {
    outputFormat = 'kotlin-website'
    outputDirectory = "$buildDir/$outputFormat"

    jdkVersion = 9

    sourceDirs = files('server/src/main/kotlin', 'plugins/src/main/kotlin')

}